tree("Root")
	repeat
		fallback
			tree("Reload")
			tree("ChasePlayer")
			tree("GoToStolenTreasure")
			tree("GoToTreasure")
			tree("Attack")
			random
				tree("Roaming")
				tree("Idle")


tree("Idle")
	while
		sequence
			not IsVisible_Treasure
			not IsVisible_StolenTreasure
			not HasEnemy
			not Acquire_Enemy
		repeat 4
			sequence
				random
					SetTarget_Angle(90.0)
					SetTarget_Angle(-90.0)
					SetTarget_Angle(135.0)
					SetTarget_Angle(-135.0)
				AimAt_Target
				Wait(2.0)


tree("Roaming")
	while 
		sequence
			not IsVisible_Treasure
			not IsVisible_StolenTreasure
			not IsVisible_Player
		sequence
			repeat 4
				sequence
					SetDestination_Waypoint
					SetTarget_Destination
					AimAt_Target
					MoveTo_Destination
					Wait(0.3)
					NextWaypoint
			sequence
				tree("ReturnToNest")
		

tree("ChasePlayer")
	while PlayerHasMarkedTreasure
		fallback
			sequence
				IsVisible_Player
				HasAmmo
				Stop
				Wait(0.5)
				SetTarget_Enemy
				AimAt_Target
				tree("Fire")
			repeat 1
				sequence
					SetDestination_Enemy
					MoveTo_Destination
			

tree("Attack")
	fallback
		repeat
			sequence
				IsVisible_Player
				HasAmmo
				Stop
				Wait(0.5)
				SetTarget_Enemy
				AimAt_Target
				tree("Fire")
		sequence
			Clear_Enemy
			Fail


tree("Fire")
	sequence
		Wait(0.3)
		Fire


tree("Reload")
	while Ammo_LessThan(3)
		sequence
			not HasAmmo
			Stop
			Wait(2.0)
			Succeed


tree("GoToTreasure")
	while not PlayerHasMarkedTreasure
		sequence
			IsVisible_Treasure
			SetDestination_Treasure
			MoveTo_Destination
			PickUp
			tree("ReturnToNest")


tree("GoToStolenTreasure")
	sequence
		IsVisible_StolenTreasure
		SetDestination_StolenTreasure
		MoveTo_Destination
		PickUp
		tree("ReturnToNest")


tree("ReturnToNest")
	sequence
		SetDestination_Nest
		Drop