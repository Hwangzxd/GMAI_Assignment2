tree("Root")
	repeat
		fallback
			sequence
				not Ammo_LessThan(3)
				tree("Attack")
			tree("Reload")
			tree("Roaming")
			tree("Idle")


tree("Idle")
	while
		sequence
			not HasEnemy
			not Acquire_Enemy
		repeat
			sequence
				random
					SetTarget_Angle(90.0)
					SetTarget_Angle(-90.0)
					SetTarget_Angle(135.0)
					SetTarget_Angle(-135.0)
				AimAt_Target
				WaitRandom(0.5, 2.0)


tree("Roaming")
	while
		sequence
			not HasEnemy
			not Acquire_Enemy
		repeat
			fallback
				sequence
					IsVisible_Treasure
					tree("GoToTreasure")
				sequence
					SetDestination_Waypoint
					SetTarget_Destination
					AimAt_Target
					MoveTo_Destination
					Wait(0.3)
					NextWaypoint


tree("Attack")
	fallback
		repeat
			sequence
				IsVisible_Enemy
				HasAmmo
				Stop
				Wait(0.5)
				SetTarget_Enemy
				AimAt_Target
				tree("Fire")
		sequence
			Clear_Enemy
			Fail


tree("FireOnSight")
	sequence
		IsVisible_Enemy
		Stop
		while IsVisible_Enemy
			repeat
				sequence
					SetTarget_Enemy
					AimAt_Target
					tree("Fire")

					//When no more ammo, run for cover sometimes
					random
						Succeed
						HasAmmo


tree("ChaseEnemy")
	while HasEnemy
		fallback
			while not IsVisible_Enemy
				parallel
					repeat
						sequence
                            WaitRandom(0.5, 1.0)
							tree("Fire")
					repeat
						sequence
							SetDestination_Enemy
							MoveTo_Destination
					sequence
						Wait(5.0)
						Fail
			tree("SearchAround")


tree("SearchAround")
	while
		sequence
			not IsVisible_Enemy
			Wait(0.5)
		sequence
			repeat(4)
				sequence
					SetDestination_Random(5.0)
					MoveTo_Destination
					Wait(0.5)
			Clear_Enemy


tree("Fire")
	sequence
		Wait(0.3)
		Fire


tree("Reload")
	while Ammo_LessThan(3)
		sequence
			not HasAmmo
			Stop
			Wait(2.0)
			Succeed


// New task to check for treasure and set destination
tree("GoToTreasure")
	sequence
		SetDestination_Treasure
		tree("ReturnToNest")


tree("ReturnToNest")
	sequence
		SetDestination_Nest
		tree("Idle")