tree("Root")
	repeat
		fallback
			tree("Charge")
			tree("Present")
			tree("Approach")
			tree("Idle")

tree("Charge")
	sequence
		IsLow
		Display("Apologies, I need to recharge.")
		MoveTo("ChargePoint")
		Charge
		sequence
			IsPresenting
			EndPresenting

tree("Present")
	sequence
		fallback
			IsClose("Player", "interact")
			sequence
				IsPresenting
				not EndPresenting
		fallback
			IsPresenting
			sequence
				QueryPlayer("Would you like me to be your exhibit guide?")
				fallback
					sequence
						IsKeyDown("Y")
						Display("Please follow me.")
						SetPlayerActive(true)
						StartPresenting
					sequence
						IsKeyDown("N")
						Display("Please enjoy the exhibits.")
						not SetPlayerActive(false)
		sequence
			fallback
				ValidExhibit
				not EndPresenting
			MoveTo("Exhibit")
			Present
			Wait(1.0)		

tree("Approach")
	sequence
		IsPlayerActive
		while IsClose("Player", "approach")
			MoveToPlayer

tree("Idle")
	sequence
		MoveTo("Entrance")
		Display("Waiting for a customer...")
		Idle
